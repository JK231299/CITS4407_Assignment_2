#!/usr/bin/env bash

# Usage: ./empty_cells <filename> <separator>

if [[ $# -ne 2 ]]; then
    echo "Usage: $0 <filename> <separator>" >&2
    exit 1
fi

filename=$1
separator=$2

awk -v FS="$separator" '
BEGIN { OFS="\t" }

NR == 1 {
    # Read headers
    for (i = 1; i <= NF; i++) {
        hdr[i] = $i
        gsub(/^\/+/, "", hdr[i])            # Remove leading slashes like /ID
        gsub(/[^[:print:]]/, "", hdr[i])    # Remove non-ASCII
        gsub(/^ +| +$/, "", hdr[i])         # Trim whitespace
        if (hdr[i] ~ /^ *$/) {
            hdr[i] = "Column_" i
        }
        count[i] = 0
    }
    num_fields = NF
    next
}

{
    # Count empty or missing fields
    for (i = 1; i <= num_fields; i++) {
        field = (i <= NF ? $i : "")
        gsub(/[^[:print:]]/, "", field)  # Remove non-ASCII
        gsub(/^ +| +$/, "", field)       # Trim leading/trailing spaces
        if (field == "") {
            count[i]++
        }
    }
}

END {
    for (i = 1; i <= num_fields; i++) {
        print hdr[i] ": " count[i]
    }
}
' "$filename"